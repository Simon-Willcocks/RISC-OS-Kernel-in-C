/* Copyright 2021 Simon Willcocks
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


ENTRY( _start )
SECTIONS
{
  /* Virtual addresses */
  va_base = 0xfc000000 ;
  shared = 0xfffef000 ; /* Make sure it doesn't overlap the workspace */
  workspace = 0xffff0000 ;
  translation_tables = 0xfff00000 ;
  task_slots = 0xfff70000 ;
  devices = 0xfff80000 ;
  rma_base = 0x20000000 ;
  rma_heap = 0x20000000 ;
  frame_buffer = 0xef000000 ;
  system_heap = 0xffe00000 ;

  _binary_AllMods_start = 0xfc0217a8 ; /* Complete hack, matches the offset of the modules in my RISC OS ROM */
  system_font = 0xfc283164 ; /* Ditto, HardFont, from vdufontl1 */

 . = va_base ;
 .text : {
  *(.text.init) ;
  . = ALIGN( 1024 ) ; /* Space for GPU to overwrite */
  *(.data) ; /* Pre-MMU data, not writable once kernel started. */
  *(.text) ;
  *(.rodata*) ;
  . = ALIGN( 4096 ) ;
  . = ALIGN( 65536 ) ; /* Ensure that the modules from the RISC OS ROM will be at their expected locations (for SharedCLib) */
  *(.modules.data*) ;
  . = ALIGN( 1024 * 1024 ) ;
  rom_size = . - va_base ;
 }

 /DISCARD/ : { *(.*) ; }
} ;
